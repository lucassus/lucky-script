program            : statements

statements         : _NEWLINE* (statement (_NEWLINE+ statement?)*)?

statement          : func
                   | return_statement
                   | expression

return_statement   : "return" expression

expression         : variable_assigment
                   | anonymous_func
                   | term (("+" | "-") term)*

variable_assigment : IDENTIFIER "=" expression

func               : "function" IDENTIFIER "(" func_args ")" block

anonymous_func     : "function" "(" func_args ")" block

func_args          : (IDENTIFIER ("," IDENTIFIER)*)?

block              : "{" statements "}"

term               : factor (("*" | "/") factor)*

factor             : ("+" | "-") factor
                   | power

power              : atom ("**" factor)*

atom               : NUMBER
                   | func_call
                   | IDENTIFIER
                   | "(" expression ")"

func_call          : IDENTIFIER "(" func_call_args ")"

func_call_args     : (expression ("," expression)*)?

%import common.WS_INLINE
%import common.NEWLINE -> _NEWLINE
%import common.CNAME -> IDENTIFIER
%import common.NUMBER

%ignore WS_INLINE
